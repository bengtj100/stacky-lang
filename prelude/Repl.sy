
[comment]: # (-*-Markdown-*-  First line intentionally left blank! -----------------------------------)

# The Stacky REPL

## Print the stack
```
0    'vertStack ;=;
1024 'maxStack  ;=;

[
    [
        dup length maxStack >
        maxStack 0 >=
        and
    ]
        [maxStack right 1]
        [0]
        ?
]'__pruneStack;
[
    "[" put
    depth toList dup
    __pruneStack [ " ..." put] [] ?
    [" " put toStr put] foreach
    " <]" putLn
    fromList drop
] '__printStackH;
[
    "[" putLn
    depth toList dup
    __pruneStack ["     ..." putLn] [] ?
    ["     " put toStr putLn] foreach
    "<]" putLn
    fromList drop
] '__printStackV;
[
    vertStack ^__printStackV ^__printStackH ?
]'__printStack;
```

## Print error messages
```
[
    swap reverse fromList drop
    [dup "" =] [drop "-"] [] ?  put ":"  put
    1 +                         put ":"  put
    1 +                         put ": " put
    "ERROR: " swap ++           putLn
]'__printError;
```

## Run the main loop and handle any errors
```
[
    "> " prompt
    
    ` Add a 1 to the stack if we succeed and a 0 if we fail
    "   1" ++ 'eval [0] catch
        [
            __printStack
        ]
        [
            __printError
            ` If the eval fails, its argument lingers on the stack.
            drop
        ]
        ?
    ^__loop inline
]'__loop;
```

### The main entrypoint
```
[
    [
        __printStack
        __loop
    ]
        [
            [dup "<stdin>: hGetLine: end of file" =]
            [
                "\nLeaving stacky interpreter.\nBye!\n" putLn
            ]
            [
                "\nstacky: ERROR: " put putLn
                "Terminating!" putLn
            ]
            ?
        ]
        catch
] '__repl;
```

### Run only if interactive
```
[__repl] ifInteractive
```
